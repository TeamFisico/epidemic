#to be tested with multiple versions
cmake_minimum_required(VERSION 3.16)
project(epidemic
        VERSION 1.0
        DESCRIPTION "SEIR model for an epidemic"
        LANGUAGES CXX)


##Prevent the mess generated by an in-source build
file(TO_CMAKE_PATH "${PROJECT_BINARY_DIR}/CMakeLists.txt" LOC_PATH)
if(EXISTS "${LOC_PATH}")
    message(FATAL_ERROR "You cannot build in a source directory (or any directory with "
            "CMakeLists.txt file). Please make a build subdirectory. Feel free to "
            "remove CMakeCache.txt and CMakeFiles.")
endif()

#allow this project to be included as a subdirectory in another project
if(CMAKE_PROJECT_NAME STREQUAL PROJECT_NAME)  #main project

    set(CMAKE_CXX_STANDARD 17)
    set(CMAKE_CXX_STANDARD_REQUIRED ON)
    set(CMAKE_CXX_EXTENSIONS OFF)

    ##SIMULATION PARAMETERS
    set(N 1000 CACHE INTERNAL "Total number of people in the simulation")
    set(W 200 CACHE INTERNAL "Total number of waypoint in the simulation")
    set(C 5 CACHE INTERNAL "Total number of clusters in the simulation")
    configure_file(
            "${PROJECT_SOURCE_DIR}/configuration.h.in"
            "${PROJECT_SOURCE_DIR}/include/epidemic3/configuration.h")

    list(APPEND CMAKE_MODULE_PATH ${PROJECT_SOURCE_DIR}/cmake)

    include(ClangFormat)

    #support IDE to show all the folders
    set_property(GLOBAL PROPERTY USE_FOLDERS ON)

endif() ##main project

find_package(SFML 2.5 COMPONENTS system window graphics network audio)

if(SFML_FOUND)
    message(STATUS "SFML package found")
endif()

find_package(ROOT 6.16 CONFIG REQUIRED)
if(ROOT_FOUND)
    message(STATUS "ROOT package found")
endif()

# Get the generate dictionary command from ROOT
if(ROOT_VERSION VERSION_LESS 6.20)
    include("${ROOT_DIR}/modules/RootNewMacros.cmake")
else()
    include("${ROOT_DIR}/RootMacros.cmake")
endif()


add_subdirectory(include/epidemic3)

# The compiled library code is here
add_subdirectory(src)

# The executable code is here
add_subdirectory(apps)

# Test code is here
add_subdirectory(tests)



## Testing only available if this is the main app
#if(BUILD_TESTING)
#    add_subdirectory(tests)
#endif()

